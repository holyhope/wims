!reset xsize ysize show_hydrogen show_carbon showfile

!set appletdir=java/WIMSchem
!set anstype=yes
!set anstyle=dprompt
!set ans_require=matchmol,checkmol,WIMSchem
!set lang__=$lang
!if $lang notwordof en fr nl
  !set lang__=en
!endif
!set wims_form_method=post

!if $wims_read_parm=def
 !exit
!endif
!set oef_js_submit=$oef_js_submit GetMoleculeMDLMol$i();

!set wims_backslash_insmath=no

!set oef_applet_option=!replace internal $	$ by $\
$ in $inputsize
!set oef_applet_option=!replace internal ; by $\
$ in $oef_applet_option
!set oef_applet_option=!nonempty lines $oef_applet_option
!set Inputsize=!line 1 of $oef_applet_option
!set test=!text remove 0123456789x in $Inputsize
!if $test issametext $empty
 !set Inputsize=!replace internal x by , in $Inputsize
 !set oef_applet_option=!line 2 to -1 of $oef_applet_option
 !distribute items $Inputsize into xsize,ysize
!endif


!default xsize=540
!default ysize=380
!for opt in show_carbon,show_hydrogen,showfile
  !set $opt=!getopt $opt in $oef_applet_option
!next

!default show_hydrogen=yes
!default show_carbon=no
!default showfile=yes

!set atoms=!getopt atoms in $oef_applet_option

!if $atoms!=
  !set atoms=!words2items $atoms
  !set atoms=!nospace $atoms
  !set atoms=<param name="atoms" value="$atoms" />
!endif
!set template=!getopt template in $oef_applet_option
!if $template notsametext
  !set templ=
  !set cnt_=0
  !if cyclo iswordof $template
   !set cyclo=cyclopropane,cyclobutane,cyclopentane,cyclohexane,cycloheptane,cyclooctane
   !for t_=1 to 6
    !set temp=!record 0 of data/chem/templ/$(cyclo[$t_]).el
    !readproc oef/togetfile.proc $(cyclo[$t_]).el new\
$temp
   !set templ=$templ\
 <param name="template$t_" value="$wims_ref_name?session=$session&+cmd=getfile&+special_parm=$(cyclo[$t_]).el" />
  !next
  !set cnt_=$[$cnt_+6]
 !endif
 !set template=!words2items $template
 !set temp_cnt=!itemcnt $template
 !for s_=1 to $temp_cnt
  !if $(template[$s_]) notsametext cyclo
   !set temp_mol=!record 0 of data/$(template[$s_])
   !if $temp_mol=
     !set temp_mol=!record 0 of data/chem/templ/$(template[$s_]).el
   !endif
   !set temp_file=!replace internal / by _ in $(template[$s_])
   !readproc oef/togetfile.proc $temp_file new\
$temp_mol
   !set cnt_=$[$cnt_+1]
   !set templ=$templ\
   <param name="template$cnt_" value="$wims_ref_name?session=$session&+cmd=getfile&+special_parm=$temp_file" />
  !endif
 !next
!endif

!set file=!getopt file in $oef_applet_option
 !if $file!=
   !set File=
   !set file=!words2items $file
   !for files in $file
     !set temp=!record 0 of $files
     !set file0=!replace internal / by , in $files
     !readproc oef/togetfile.proc $(file0[-1]) new\
$temp
     !set File=!append line <param name="file" value="$wims_ref_name?session=$session&+cmd=getfile&+special_parm=$(file0[-1])" /> to $File
   !next
 !endif

!set list=TOOL_CURSOR,TOOL_ROTATOR,TOOL_ERASOR,TOOL_DIALOG,TOOL_EDIT,\
TOOL_SETATOM,TOOL_SINGLE,TOOL_DOUBLE,TOOL_TRIPLE,TOOL_ZERO,\
TOOL_INCLINED,TOOL_DECLINED,TOOL_UNKNOWN,TOOL_CHARGE,TOOL_UNDO,TOOL_REDO,\
TOOL_TEMPLATE,TOOL_CUT,TOOL_COPY,TOOL_PASTE,TOOL_SELECT,\
MENU_BLOCK,MENU_SELECT,MENU_TRANSFORM,MENU_ZOOM,MENU_SHOW,MENU_HYDROGEN,MENU_STEREO
!set list_m=!lower $list
!set list_default=1,0,1,0,0,\
1,1,1,1,0,\
0,0,0,0,1,1,\
1,0,0,0,0,\
1,1,0,0,0,0,0
!set listcnt=!itemcnt $list
!set params=
!for u_ =1 to $listcnt
 !set a_=$(list_m[$u_])
 !set a__=$(list[$u_])
 !set b_=!getopt $a_ in $oef_applet_option
 !bound $b_ within yes, no, 1, 0
 !if $b_=
   !set b_=$(list_default[$u_])
 !endif
 !set params=!append line <param name="$a__" value="$b_" /> to $params
!next
<script type="text/javascript">
/*<![CDATA[*/
 function GetMoleculeMDLMol$i(){
   var wimschem = document.getElementById('WIMSchem$i');
    <!-- reading the WIMSchem file format from a drawing and display it in textarea "moltext" -->
   var input1=document.WIMSchem$i.GetMoleculeMDLMol();
   var result=document.WIMSchem$i.ReadApplet();
   var input2=result.split('\n');
!if svg iswordof $(replyoption$i)
   var rep_svg = document.WIMSchem$i.getSVG('1');
   wimschem.stop();wimschem.destroy();
   document.getElementById('WIMSchemreply$i').value = rep_svg + '\n' + input2[1] + '\n' + input1;
!else
   wimschem.stop();wimschem.destroy();
   document.getElementById('WIMSchemreply$i').value = input2[1] + '\n' + input1;
!endif
}
/*]]>*/
</script>

<input type="hidden" name="reply$i" id="WIMSchemreply$i" value="" />
!if help isin $(replyoption$i)
 !set help_option=!getopt help in $(replyoption$i)
 !set help_option=!replace internal help by in $help_option
 !set help_option=!declosing $help_option
 !set help_option=!words2items $help_option
 !set help_option=!nonempty items $help_option
  <div style="text-align:right; width:60%">
 !read anstype/chemdraw.inc $help_option
 </div>
!endif

<object name="WIMSchem$i" id="WIMSchem$i" type="application/x-java-applet;jpi-version=1.4" \
 classid="java:WIMSchem.MainApplet" width="$xsize" height="$ysize">
  <param name="java_archive" value="WIMSchem.jar" />
  <param name="java_codebase" value="$appletdir" />
  <param name="java_code" value="WIMSchem.MainApplet" />
  <param name="showfile" value="$showfile" />
  <param name="show_hydrogen" value="$show_hydrogen" />
  <param name="show_carbon" value="$show_carbon" />
  <param name="language" value="$lang__" />\
  <param name="zoomfactor" value="1.5">\
  $templ
  $atoms
  $params
  $File
</object>

