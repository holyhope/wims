
!if $error!=$empty
 !exit
!endif

!if $no_selection!=$empty
 <b>$wims_name_warning</b>. $name_noselect
 <br />
!endif

!if $serial!=$wims_class and $old_testcnt>0
 !set test=bad
!endif

!if $test=bad
<div class="wims_msg alert">
<span class="wims_warning">$wims_name_warning!</span>
!read lang/names.phtml.$moduclass_lang arch
</div>
!! ?? nsgmls:E: document type does not allow element "HR" here
!! <hr />
!endif

!form reply
<input type="hidden" name="job" value="$job" />
<input type="hidden" name="job2" value="restore" />
$name_deposit
<ul>
 <li>
  <input type="checkbox" name="selection" value="all" />
  $name_all
 </li>
 !if def isitemof $gotlist
  <li>
   <input type="checkbox" name="selection" value="def" />
   $name_config.
   $name_danger.
  </li>
 !endif
 !if sup isitemof $gotlist
  <li>
   !if $sharing_sup=$empty
    <input type="checkbox" name="selection" value="sup" />
   !endif
   $name_teacher.
   !if $sharing_sup!=$empty
    $name_no_restore
   !else
    $name_danger.
   !endif
  </li>
 !endif
 !let list=!listintersect user,doc,exo,sheet,exam,forum,motd and $gotlist
 !for type in $list
  <li>
   !if $(sharing_$type)=$empty
    <input type="checkbox" name="selection" value="$type" />
   !endif
   $(new_$(type)cnt) $(name_$type).
   !if $(sharing_$type)!=$empty
    $name_no_restore
   !else
    <span class="small">($(name_$(type)1) : $(old_$(type)cnt))</span>.
   !endif
  </li>
 !next type
 !if score isitemof $gotlist
  <li>
   <input type="checkbox" name="selection" value="score" />
   $name_activity
  </li>
 !endif
 !if manual isitemof $gotlist
  <li>
   <input type="checkbox" name="selection" value="manual" />
   $new_manualcnt $name_manual. <span class="small">($name_manual1&nbsp;:
   $old_manualcnt)</span>.
  </li>
 !endif
 !if cdt isitemof $gotlist
  <li>
   <input type="checkbox" name="selection" value="cdt" />
   $name_cdt.
   !if $old_cdtcnt>0
    <span class="small">($name_cdt1) </span>
   !endif
  </li>
 !endif
 !if livret isitemof $gotlist
  <li>
   <input type="checkbox" name="selection" value="livret" />
   $name_livret.
   !if $old_livretcnt>0
    <span class="small">($name_livret1)</span>
   !endif
  </li>
 !endif
 !if seq isitemof $gotlist
  <li>
   <input type="checkbox" name="selection" value="seq" />
   $name_seq.
   $name_seq0
   <span class="small">($name_seq1 $old_seqcnt)</span>.
  </li>
 !endif
</ul>
$name_method&nbsp;:
<ul>
 <li><input type="radio" name="restore" value="replace" checked="checked" />
  $name_replace <i>($name_replace1)</i>.</li>
 <li><input type="radio" name="restore" value="merge" />
  $name_merge <i>($name_merge1)</i>.</li>
</ul>
!!## TODO desactiver les feuilles s'il n'y pas de participant et enlever la date d'expiration?
!!!if $(new_usercnt)=0
!!   <input type="checkbox" name="preparation" value="no" /> Mettre les feuilles en préparation
!!!endif
<input type="submit" value="$name_selectrestore" />
!set wims_ref_class=wims_button_help
!href cmd=reply&job=list $wims_name_giveup


!formend

<div class="wims_alertmsg">
!read lang/names.phtml.$moduclass_lang arch_end
</div>
