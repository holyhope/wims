!goto $wims_read_parm

:def
title=Courbe paramétrée à implicite 2D
synonyme=equation implicite 2d, 2d equation implicite, parametree a implicite 2D, equation implicite d'une courbe plane
input=list
!exit

:proc
formula=!nonempty item $formula
cnt=!itemcnt $formula
!if $cnt!=2
 error=bad_formula
 !exit
!endif
v1=!varlist nofn $formula
v2=!varlist $formula
n1=!itemcnt $v1
n2=!itemcnt $v2
!if $n2>$n1 or . isin $formula
 result=not_polynomial
 !exit
!endif
!if $n1<1
 result=no_parameter
 !exit
!endif
w1=!item 1 of $v1
fml=$formula
!if t notitemof $v1
 fml=!mathsubst $w1=t in $fml
!endif

!distribute items $fml into fx,fy
!distribute items $formula into Fx,Fy
m2_header=implicit2d = (f1,f2) -> (\
     -- f1 and f2 should be polynomials over QQ in a variable\
     -- t.  A string representation of the polynomial\
     -- F(x,y) is returned, where F(f1,f2) = 0.\
     R = QQ[t,x,y,h,MonomialSize=>16,MonomialOrder=>Eliminate 1];\
     f = value f1;\
     g = value f2;\
     J = ideal(x-f,y-g);\
     Jh = homogenize(J,h);\
     M = selectInSubring(1,gens gb Jh);\
     toString substitute(M_(0,0), {h=>1}))

result=!exec m2 implicit2d("$fx","$fy")
result=!translate # to $\
$ in $result
result=!line 1 of $result
# Here I just discard the first two words of the result
result=!word 3 to -1 of $result
!exit

:output

!if $result=not_polynomial
 Désolé, mais nous ne pouvons calculer les équations que quand les fonctions
 paramétrées sont des polynômes de coefficients rationnels.
 !exit
!endif
!if $result=no_parameter
 Désolé, mais êtes-vous sûr que vous avez entré deux polynômes du paramètre t
 comme des fonctions de coordonnées paramétrées&nbsp;?
 !exit
!endif

La courbe plane définie par les fonctions paramétrées
<p><center>
!htmlmath x = $Fx , y = $Fy
<p></center>
satisfait l'équation implicite
<p><center>
!insmath $result = 0 .
</center> <p>
<small>Calcul effectué par Macaulay 2</small>
!exit

