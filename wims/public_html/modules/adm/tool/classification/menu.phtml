!read adm/language lang/names.phtml

!! menu definition file
!if $error!=$empty
  <div class="wims_msg alert">
    !read msg.phtml
  </div>
  !exit
!endif
$name_intro
!form reply id="classifications"

!! Tab links
<ul>
  !for t=1 to $ref_list_cnt
    !set tt=$(ref_list[$t])
    !if $tt!=keywords
      !set name_$tt=!defof title in keywords/$tt.phtml.fr
    !endif
    <li><a href="#bloc_$tt">$(name_$tt)</a></li>
  !next
</ul>

!for t=1 to $ref_list_cnt
  !set tt=$(ref_list[$t])
  <fieldset id="bloc_$tt" class="property_fields blockcenter">

    !set ref_$(tt)_cnt=!itemcnt $(ref_$tt[;1])
    !set ref1_$(tt)=!lines2rows $(ref_$(tt))
    !if  $(ref_$(tt)_cnt)>0
      <div class="field box">
        <p>$name_evaluation</p>
        <ul>
          !for j = 1 to $(ref_$(tt)_cnt)
            !if $(ref1_$tt[$j;1])!=$empty
              !set key=$(ref1_$(tt)[$j;1])
              !reset fullkey
              !if $tt issametext keywords
                !set fullkey=!lookup $key in wimshome/public_html/bases/sys/domain/domain.$lang
                !default fullkey=$key
              !else
                !set keycut=!replace internal _ by , in $key
                !set keycutcnt=!itemcnt $keycut
                !set asc=$(keycut[1])_$(keycut[2])
                !set fullkey=!lookup $asc in $taxodir/$tt.fr
                !for s=3 to $keycutcnt
                  !set asc=$(asc)_$(keycut[$s])
                  !set keyasc=!lookup $asc in $taxodir/$tt.fr
                  !set fullkey=$fullkey > $keyasc
                !next
              !endif
            <li>
          <span class="small">$fullkey</span><br/>
          !set evl=!declosing $(ref1_$(tt)[$j;2])
            !set wims_ref_class=wims_formradio
            !formradio f_$(tt)_$j list -1,0,1 prompt \
              $(name_prompt[1]) ($[-$(evl[1])]),\
              $(name_prompt[2]),\
              $(name_prompt[3]) ($(evl[2])),
            </li>
            !endif
          !next
        </ul>
      </div>
    !endif

    <div class="field box">
    <p>
    $(name_$(tt)_instruction)
    </p>
     !if $tt issametext keywords
     !! lit le fichier dans scripts/help
        !read help/keywords.phtml radio
        <p>
         <input type="radio" name="taxon_keywords" id="empty" value=""/>
         $wims_name_erase
        </p>
        <input type="hidden" name="search_lang" value="tech" id="search_lang" />
        <label for="taxon_$tt">$name_addkeyword_label</label>
        <input name="taxonfree_$tt" id="taxonfree_$tt" class="multicomplete" placeHolder="$name_onewordmax" />
        <script type="text/javascript" src="scripts/js/interface/search_engine.js"></script>
        <div class="formHelp">$name_addkeywords</div>
    !else
      !if $(ref_address_$tt)!=$empty
       <a class="float_right wims_button_help" target="wims_external" href="$(ref_address_$tt)">$wims_name_help</a>
      !endif
      <p>$name_selectkeyword</p>
       !read keywords/$tt.phtml.$lang
    !endif
    </div>
  </fieldset>
!next
<div class="wimsform wimscenter">
 <input type="submit" value="$wims_name_tosave" />
</div>

!formend
