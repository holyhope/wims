<!-- adm/sheet/class.phtml START -->
!readdef wimshome/log/classes/$wims_class/.def
!read adm/css.phtml
!header1

!if $sheet_docpage!=$empty
 !changeto docpage.phtml
!endif
!read adm/class/classname.phtml $wims_classname\
$wims_institutionname

!if $seq=$empty
  !read adm/title.phtml 1\
$classname\
$sheet_title \
$name_title
!else
  !read adm/title.phtml 1\
$classname\
$sheet_title
!endif

<div class="wims_sheet_desc">$sheet_desc</div>
!let class_showlivret=!defof class_showlivret in wimshome/log/classes/$wims_class/.def
!let tmp=!record $sh of wimshome/log/classes/$wims_class/livret/sheetindex.rc
!if $class_showlivret=yes and $tmp!=$empty
 <div class="wims_sheet_desc">
  $name_skillbullet $tmp
 </div>
!endif
!!expiration date in a variable
!set expir_user=!record 0 of wimshome/log/classes/$wims_class/.$sh

!set cle=!word 1 of $expir_user
!! process for technical variable definition (replace expir_user by value decide by the technical variable)
!if \ isin $cle
   !set idtechvar=!line 1 of $expir_user
   !set idtechvar=!char 2 to -1 of $idtechvar
   !readproc adm/class/userdef ,$wims_class,$wims_user
   !set val=!defof user_techvar_$(idtechvar) in $userdef
   !default val=EMPTY
   !reset expir_user
   !set expir_user=!defof techvar_$val in wimshome/log/classes/$wims_class/.$sh
   !default expir_user=none
!endif
!! end process technical variable

!if $expir_user=none
 <p id="name_student5">
  <span class="wims_warning">$wims_name_warning</span>.
  $name_student5
 </p>
 !goto none
!endif

!if $expir_user!=$empty
  !set cle=!word 1 of $expir_user
!!! a corriger
  !set expir_user=!words2items $expir_user
   !for l_ in $expir_user
     !set l_=!replace internal < by ?, in $l_
     !if ? isitemof $l_
      !set sheet_expiration_user=$(l_[2])
     !endif
   !next
   !for l_ in $expir_user
     !set l_=!replace internal > by ?, in $l_
     !if ? isitemof $l_
      !set sheet_beginning_user=$(l_[2])
     !endif
   !next
!endif

!if $sheet_beginning_user!=$empty and $sheet_active=1
  !set expday=!char 7,8 of $sheet_beginning_user
  !set expmon=!char 5,6 of $sheet_beginning_user
  !set expyear=!char 1 to 4 of $sheet_beginning_user
  !set exphour=!char 10 to -1 of $sheet_beginning_user
  !if $exphour!=
   !set exphour=$ ($exphour)
  !endif
  !read adm/date.phtml
  !set expmon=!item $expmon of $months
  !set fromdate=$name_from $expday $expmon $expyear$exphour
!endif

!default sheet_expiration_user=$sheet_expiration
!if $sheet_expiration_user!=$empty and $sheet_active=1
 !set expday=!char 7,8 of $sheet_expiration_user
 !set expmon=!char 5,6 of $sheet_expiration_user
 !set expyear=!char 1 to 4 of $sheet_expiration_user
 !set exphour=!char 10 to -1 of $sheet_expiration_user
 !if $exphour!=
   !set exphour=$ ($exphour)
 !endif
 !read adm/date.phtml
 !set expmon=!item $expmon of $months
 <p id="name_student1">$name_student1$exphour</p>
!endif

:none
!if $sheet_active=2
 <p id="name_student2">
 <span class="wims_warning">$wims_name_warning</span>.
 $name_student2</p>
!endif

!if $n=0
 !if $sheet_active=$empty
  $name_sheetempty
 !else
 <span class="name_student4">$name_student4</span>
 !endif
!else
  !set wims_menu_items=!append line printable,1,cmd=reply&job=print\
  itemsep,0,\
to $wims_menu_items
  !if $exolog!=$empty
    !set wims_menu_items=!append line exolog,1, module=adm/class/exolog&job=llist&logsheet=$sh \
itemsep,0,\
 to $wims_menu_items
 !endif
 !set scorestat=!getscorestatus sheet=$sh
 !if $scorestat=yes
  !if $authscoresuspend!=no
   !if $wims_scorereg!=suspend
    $name_scoreopen
    !href cmd=resume&job=suspend $wims_name_scoreclose2
.
    !set wims_menu_items=!append line scoreclose2,1,cmd=resume&job=suspend to $wims_menu_items
   !else
    !set scorestat=no
    $wims_name_scoresuspend
    !href cmd=resume&job=reopen $wims_name_scorereopen
.
    !set wims_menu_items=!append line scorereopen,1,cmd=resume&job=reopen to $wims_menu_items
   !endif
  !else
   $name_scoreclosesup
  !endif
 !else
  $name_scoreclose
 !endif
 !set sevlist=!record 0 of wimshome/log/classes/$wims_class/sheets/.severity
 !set sev=!line $sh + 1 of $sevlist
 !reset s ss
 !set ss=!word 3 of $sev
 !set tmp=!defof DF_SEVERITY in wimshome/public_html/bases/sys/define.conf
 !set tmp=!word 3 of $tmp
 !bound ss between integer 0 and 2 default $tmp
 !reset tmp
 <div class="wims_sheet">
  <div class="wims_title_seq">$wims_name_Exercices</div>
 <ol class="wims_sheet_list">
 !let wims_exotitle=$empty
 !for i=1 to $n
  !set ex=!record $i of wimshome/log/classes/$wims_class/sheets/.sheet$sh
  !distribute lines $ex into di,pa,require,we,ti,de
  !set got=!word $i of $scores
  !set mean=!word $i of $means
  !set best=!word $i of $bests
  !set level=!word $i of $levels
  !set dep=!row $i of $deps
  !set dep=!trim $dep
  !set wims_exotitle=!append line $ti to $wims_exotitle
  <li class="wims_exo_item">
  !if $dep=$empty or $scorestat!=yes
   !href module=$di&cmd=new&worksheet=$sh&$pa $ti
   !set last=!char -1 of $de
   !if $last notin .!?
     !set de=$(de).
   !endif
 <span class="wims_work_desc">$de</span>
   <div class="wims_user_info">
     !if $require>0
       <span class="wims_sheet_score small">
       $(wims_name_thsheet[7]): $[$mean]/10,
       !if $ss=0
         $(wims_name_thsheet[5]): $[round(100*$got/$require)]%,
       !endif
       !if $ss=1
         $(wims_name_thsheet[10]): $[round(100*$best/$require)]%
       !endif
       !if $ss=2
         $(wims_name_thsheet[13]): $[$level]/10
       !endif
       $(wims_name_thsheet[3]):$require
       </span>
      !endif
    </div>
  !else
   <span class="wims_warning">$ti</span>, <span class="wims_work_desc">$de</span>
   !if $require>0
    <span class="small">($name_score2)</span>
   !endif
  !endif
  !if $dep!=$empty
   <span class="small">
   !if , isin $dep
    $name_deps $dep
   !else
    $name_dep $dep
   !endif
   $name_dep2
   </span>
  !endif
 </li>
 !next i
 !let wims_exotitle=!replace , by &#44; in $wims_exotitle
 !let wims_exotitle=!lines2items $wims_exotitle
 !setdef wims_exotitle=$wims_exotitle\
wims_sheettitle=$sheet_title in wimshome/sessions/$wims_session/var.stat
 </ol>
 </div>
!endif

!if $wims_user=supervisor
 !set wims_menu_items=!append line sheetmanagement,1,module=adm/class/sheet&sheet=$sh\
 itemsep,0,\
to $wims_menu_items
!endif

!set wims_menu_items=!append line myscore,1,module=adm/class/userscore#sheet$sh\
to $wims_menu_items

<!-- adm/sheet/class.phtml END -->

!tail

