!! duplicate a sheet

!if $scnt>=$max_sheets
    error=bad_maxsheets
    !reset job wims_ariane
    !changeto var.proc
!endif
!bound sheet between 1 and $scnt default $empty

!if $sheet=$empty
    error=bad_sheet
    !reset job wims_ariane
    !changeto var.proc
!endif

exocnt=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheet$sheet
!if $exocnt=0
 !reset job wims_ariane
 !changeto var.proc
!endif

!sh cd $wims_home/log/classes/$wims_class/sheets/\
cp .sheet$sheet .sheet$[$scnt+1]

p=!record $sheet of wimshome/log/classes/$wims_class/sheets/.sheets
p=!replace line number 1 by 0 in $p

!appendfile wimshome/log/classes/$wims_class/sheets/.sheets :$p

!! --- copy severity of the sheet:
fsev=!record 0 of wimshome/log/classes/$wims_class/sheets/.severity
sev=!line $[$sheet+1] of $fsev
!if $sev!=$empty
 n=!linecnt $fsev
 !while $n<$scnt+1
  fsev=$fsev\

  !increase n
 !endwhile
 fsev=!append line $sev to $fsev
 !writefile wimshome/log/classes/$wims_class/sheets/.severity $fsev
!endif
!! ---

scnt=$[$scnt+1]
sheet=$scnt
message=$name_duplicateok
!reset job wims_ariane
!changeto var.proc
