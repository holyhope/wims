!!##language independant
!set wims_form_method=post
!read adm/title.phtml 1\
$classname\
$name_severitylevels

!form reply
<input type="hidden" name="job" value="formula" />
$name_formula_comment
<input size="5" name="maxscore" value="$scoremax" />.
$table_header
$table_hdtr<th>$(name_formula[1])</th><th>$wims_name_title</th><th>$wims_name_Status</th>
  <th>$(name_formula[2])</th><th>$(name_formula[3])</th></tr>
!for i=1 to $totsheets
 !set t_=!record $i of wimshome/log/classes/$wims_class/sheets/.sheets
 !set k_=!line $i of $defaultformula
 !distribute word $k_ into w$i,s$i,ss$i
 !default w$i=1
 !default s$i=2
 !default ss$i=0
 !distribute line $t_ into a_,b_,c_,d_
 !set a_=!char 1 of $a_
 !set S_=!item $a_+1 of $name_sheetstatut

 $table_tr<td style="text-align:right">$i</td>
 <td>$c_</td><td>$S_</td>
 <td><input size="5" name="w$i" value="$(w$i)" /></td>
 <td>
 !set formula_list=!record 0 of adm/class/sheetformula
 !formselect s$i list 0,1,2,3,4,5,6 prompt $formula_list
 !formselect ss$i list 0,1,2 prompt I0,I1,I2
 </td></tr>
!next i
!for i=1 to $totexams
 !set ew$i=!line $i of $examweights
 !default ew$i=1
 !set t_=!record $i of wimshome/log/classes/$wims_class/exams/.exams
 !set a_=!word 1 of $t_
 !set c_=!line 4 of $t_
 !set S_=!item $a_+1 of $name_sheetstatut

 $table_tr<td style="text-align:right">$name_Exam $i</td>
 <td>$c_</td><td>$S_</td>
 <td><input size="5" name="ew$i" value="$(ew$i)" /></td>
 <td>--</td>
 </tr>
!next i
$table_end
<div class="wimscenter wimsform">
<input type="submit" name="formula" value="$wims_name_tosave" />
&nbsp;<input type="submit" value="$wims_name_giveup" />
</div>

!formend

!reset formula
