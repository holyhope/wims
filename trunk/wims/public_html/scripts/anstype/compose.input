!set anstype=yes
!set anstyle=symtext
!set ans_require=dynapi3

!if $wims_read_parm=def
 !exit
!endif
!bound inputsize between integer 1 and 40 default 10
!if $inputsize<=5
 !distribute item '<p class=\"center\">' +,+ '</p>' into ct1,ct2
!else
 !reset ct1,ct2
!endif
!set linkword=!getopt linkword in $(replyoption$i)
!default linkword=$ $
!if $linkword=empty
  !reset linkword
!endif
!set oef_answer_option$i=$linkword
!set inputsize=$[$inputsize*10]
!set lenlimit=40
!set imgs=$(replygood$i)
!set imgs=!translate internal | to , in $imgs
!set imgs=!rows2lines $imgs
!set atest=!line 1 of $imgs
!if ?analyze iswordof $atest
 !set imgs=!line 2 to -1 of $imgs
!endif
!set imgs=!lines2items $imgs
!set imgs=!singlespace $imgs
!set imgs=!nonempty items $imgs
!set imgs=!listuniq $imgs
!set imgs=!sort nocase items $imgs
!set imgcnt=!itemcnt $imgs
!set imgl=!items2lines $imgs
!set imgarr=!replace internal " by \" in $imgl
!set imgarr=!replace internal ' by \' in $imgarr
!set imgarr=!replace internal $\
$ by ',' in '$imgarr'
!set Imgarr=!replace internal \ by \\ in $imgl
!set Imgarr=!replace internal " by \" in $Imgarr
!set Imgarr=!replace internal ' by \' in $Imgarr
!set Imgarr=!replace internal $\
$ by ',' in '$Imgarr'
!set wims_ins_alt=none
!set z=C$i

!read js/dynapi3.phtml
<script type="text/javascript">
/*<![CDATA[*/
 var $(z)_imgcnt=0;
 $(z)_imgs=new Array($imgarr);
 $(z)_Imgs=new Array($Imgarr);
 $(z)_strs=new Array($lenlimit+1);
 $(z)_Strs=new Array($lenlimit+1);
 $(z)_strs[0]='';
 $(z)_Strs[0]='';
 var lyr$i;

dynapi.onLoad(function() {

	lyr$i=new DynLayer();
	lyr$i.setAnchor({topA:'tarea$i',leftA:'tarea$i',stretchH:'70%',stretchV:$inputsize});
	lyr$i.setBgColor('#c0c0c0');
	lyr$i.setHTML('');
	dynapi.document.addChild(lyr$i);
});

 function $(z)_addone(t) {
  if(t<0 || $(z)_imgcnt>=$lenlimit) return;
  if($(z)_imgcnt>0) {
    $(z)_strs[$(z)_imgcnt+1]=$(z)_strs[$(z)_imgcnt]+'$linkword'+$(z)_imgs[t];
    $(z)_Strs[$(z)_imgcnt+1]=$(z)_Strs[$(z)_imgcnt]+' '+$(z)_Imgs[t];
  }
  else {
    $(z)_strs[$(z)_imgcnt+1]=$(z)_imgs[t];
    $(z)_Strs[$(z)_imgcnt+1]=$(z)_Imgs[t];
  }
  $(z)_imgcnt++; $(z)_lockdata();
 }

 function $(z)_backone() {
  if($(z)_imgcnt<1) return;
  $(z)_imgcnt--; $(z)_lockdata();
 }

 function $(z)_cleanall() {
  if($(z)_imgcnt<1) return;
  $(z)_imgcnt=0; $(z)_lockdata();
 }

 function $(z)_lockdata() {
  lyr$i.setHTML($ct1 $(z)_strs[$(z)_imgcnt] $ct2);
  document.forms['replyform'].reply$i.value=$(z)_Strs[$(z)_imgcnt];
 }
/*]]>*/
</script>

!reset wims_ins_alt
!if $wims_read_parm!=noprompt
<tr><td>
 !if $module_language=fr
  Cliquez sur les objets ci-dessous pour composer votre réponse.
  !goto cont
 !endif
 !if $module_language=nl
 Klik voor het antwoord op de onderstaande objecten
  !goto cont
 !endif

 Click on the objects below to compose your reply.
 :cont
!endif
!if $wims_read_parm!=noprompt and $(replyname$i) notsametext
 <p><label for="reply$i">$(replyname$i)</label></p>
!else
  <br class="spacer" />
!endif

<div class="wimscenter">
<a id="tarea$i">
<img style="height:2px;width:70%;vertical-align:middle;" src="gifs/transparent.gif" alt="transparent" />
</a> <br />
<img style="height:$[$inputsize-10]px;width:70%;vertical-align:middle;" src="gifs/transparent.gif" alt="transparent" />
</div>

!set instex_color=blue
!for i_=1 to $imgcnt
 !set src=!item $i_ of $imgs
 <a href="#" onclick="$(z)_addone($i_-1)">$src</a>&nbsp;
!next i_

!set instex_color=black
!set style_=height:20px;vertical-align:middle;
<a href="#" onclick="$(z)_backone()"><img style="$style_" src="gifs/arrows/left3.32.gif" alt="left" /></a>
<a href="#" onclick="$(z)_cleanall()"><img style="$style_" src="gifs/reset.gif" alt="reset" /></a>

<input type="hidden" name="reply$i" value="" />

!if $wims_read_parm!=noprompt
  </td></tr>
!endif
