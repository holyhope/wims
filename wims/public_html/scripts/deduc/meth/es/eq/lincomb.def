!set methtit=Addition d'équations avec coefficients
!set methparmtype=parm equation >,parm equation >,\
	number, number, choice 2
!set methparmrelax=5
!set methhelp=Cette méthode calcule la somme avec coefficients de deux équations.\
	Le résultat peut soit être rajouté aux hypothèses, soit remplacer\
	l'une des deux équations de départ.

!if $wims_read_parm iswordof form check
 !goto $wims_read_parm
!endif

!exit
:form
 Introduire une nouvelle équation qui est <br>
 !default methparm3=1
 !default methparm4=1
 !read deduc/methparm.phtml 3,5
 fois l'équation
 !read deduc/methparm.phtml 1
 <br> plus
 !read deduc/methparm.phtml 4,5
 fois l'équation
 !read deduc/methparm.phtml 2
 <br>
 Cette nouvelle équation va
 !formselect methparm5 list 0,1,2 prompt \
	être rajoutée dans les hypothèses,\
	remplacer la première,\
	remplace la deuxième
!exit
:check
 !ifval $methparm3=0 and $methparm4=0
  error=Les coefficients sont nuls !
  !exit
 !endif
 !ifval ($methparm=1 and $methparm3=0) or \
	($methparm=2 and $methparm4=0)
  error=Vous voulez remplacer une équation dont le coefficient dans\
	la somme est nul&nbsp;? Cela vous fera perdre de\
	l'information&nbsp;!
  !advance penalty
  !exit
 !endif
 !distribute items $methparmobj1 into data1,left1,sign1,right1
 !distribute items $methparmobj2 into data2,left2,sign2,right2
 !if $data1 issametext $data2
  error=Cette méthode interdit l'addition d'une équation avec elle-même.
  !exit
 !endif
 methexp=\(($methparm3)*($left1 $sign1 $right1) + ($methparm4)*($left2 $sign2 $right2))
 
 !read deduc/sub/simplify ($methparm3)*($left1) + ($methparm4)*($left2)\
	($methparm3)*($right1) + ($methparm4)*($right2)
 !distribute lines $out into newleft, newright
 !if $newleft=$empty or $newright=$empty
  error=bad_data
  !exit
 !endif

 newobject$methparm5=$newleft = $newright
 oldobject=$methparm5
 forcereplace=$methparm5

!exit

