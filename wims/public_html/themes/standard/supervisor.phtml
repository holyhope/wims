<!-- begin of $wims_theme/supervisor.phtml -->
!! supervisor page definition.
!nocache
!if $module!=home
 !exit
!endif
!set home_theme=yes

!set table_spacer=no
!readproc tabletheme

!read classlogo.phtml
!if $class_logo!=
  <div id="classlogo" class="float_$class_logoside">
   <img src="$class_logo" alt="class logo" />
  </div>
!endif
!! classtype=0 or 1
<div id="wims_class_manager_msg">
 !read ./wimshome/log/manager_msg.phtml.$lang
</div>
!read ./adm/title.phtml 2\
$wims_classname, $wims_institutionname\
($SU_log - $(wims_name_$class_typename))
<br class="clearall" />

!if _tool isin $wims_session
 !read ./tools.phtml
 </div><!--wimsbody-->
</div><!--wimspagebox-->
 !goto end
!endif
!!definition for menumodubox
!distribute items div,div into div0,div1
!if $class_type isin 3
 <p class="wims_greetings">
   $SU_greet.
 </p>
 !read Subclasses1.phtml
 !if $class_typename=level
    </div><!--/wimsbody-->
   </div><!--/wimspagebox-->
   !read themes/_procs/menumodubox3.proc
   !read themes/_widgets/menumodubox.phtml
  !goto end
 !else
  !goto nocheck
 !endif
!endif class_type = 3

!if $class_type notin 13
 <span class="wims_greetings">
 $SU_greet
 $SU_exp1
 !set d_=!char 7,8 of $class_expiration
 !set m_=!char 5,6 of $class_expiration
 !set y_=!char 1 to 4 of $class_expiration
 !read adm/date.phtml date,$y_,$m_,$d_
 !href module=adm/class/config $c_date_out
 $SU_exp2.
 </span>
!else
 <span class="wims_greetings">
  $SU_greet.
 </span>
!endif

!set wims_ref_class=wims_button_help
!href target=wims_help module=help/main&chapter=3&open=3_sheet#sheet $wims_name_help

!if $wims_numpartconnected>0
 <p class="wims_connected">$SU_numpartconnected.</p>
!else
 <p>$SU_nopartconnected.</p>
!endif

!if $quota_free<2
 !if $quota_free<0
  <p>$wims_name_exceeded</p>
  <p>
  !href cmd=close&module=home&lang=$lang $wims_name_visitor
  </p>
 </div><!--wimsbody-->
</div><!--wimspagebox-->
  !goto end
 !else
   <br /><span class="wims_warning">$wims_name_warning</span>! $wims_name_reaching_limit
 !endif
!endif
:nocheck

!if & isin $newmsgcnt or $newmsgcnt>0
 <br />
 !href module=adm/forum/mboard $U_newmsg
!endif

!if $class_type=4
 </div><!--/wimsbody-->
</div><!--/wimspagebox-->

 !read themes/_procs/menumodubox3.proc
 !read themes/_widgets/menumodubox.phtml
 !goto end
!endif class_type=4
!if $class_type=2
 !if $subclasscnt>0
  $table_header
  $table_hdtr<th>$wims_name_number</th><th>$wims_name_title</th><th>$wims_name_nameteacher</th></tr>
  !for i=1 to $subclasscnt
   !let f_class=!record $i of wimshome/log/classes/$wims_class/.subclasses
   !let f_aff=!item 4 of $f_class
   !let f_name=!item 1 of $f_class
   $table_tr
    <td>$ADD_class $i</td>
    <td>
    !href module=adm/class/classes&type=authsupervisor&class=$f_name $f_aff
    </td>
    <td>
     !item 9 of $f_class
    </td>
   </tr>
  !next i
  $table_end
 !endif subclasscnt

 </div><!--wimsbody-->
</div><!--wimspagebox-->

 !read themes/_procs/menumodubox2.proc
 !read themes/_widgets/menumodubox.phtml
 !goto end
!endif class_type=2

!set test=!replace / by , in $wims_class
!if $(test[-1])=0
  !set test_up=!replace internal /0- by in $wims_class-
  !set wims_ref_class=wims_button
  !href module=adm/class/classes&type=authsupervisor&class=$test_up $SU_Bprogram
!endif

 <div id="wims_supervisor_search">
!read ./form.phtml
 <br class="clearall" />
!read ./result.phtml
 </div>

!set docpubliccnt=!recordcnt  wimshome/log/classes/$wims_class/doc/.docindex

!if $sheetcnt+$examcnt+$doccnt+$docpubliccnt+$votecnt+$subclasscnt<=0
 <p>$SU_nosheet</p>
!else
 !set seq_open=!defof sequence_open in wimshome/log/classes/$wims_class/seq/.def
 !if $seq_open=yes
   $CR_sequence
 !endif
 <div class="wimscenter">
 !href module=adm/class/sequence $wims_name_reorder
 </div>
 <div id="widget_supervisor_list">
 !if $jquery_defined=yes
 !read adm/tabscript supervisor_list
 !endif
 <ul id="wims_supervisor_summary">
 !if $subclasscnt>0
  <li><a href="#wims_supervisor_subclass">$wims_name_classlist</a></li>
 !endif
 !if $[$docpubliccnt+$doccnt]>0
  <li><a href="#wims_supervisor_doc">$wims_name_Docs</a></li>
 !endif
 !if $sheetcnt>0
  <li><a href="#wims_supervisor_sheet">$wims_name_Sheets</a></li>
 !endif
 !if $examcnt>0
  <li><a href="#wims_supervisor_exam">$wims_name_Examens</a></li>
 !endif
 !if $votecnt>0
  <li><a href="#wims_supervisor_vote">$wims_name_Votes</a></li>
 !endif
 </ul>
 !if $subclasscnt>0
   <div id="wims_supervisor_subclass">
    !distribute item 1,no into wims_incremental,table_spacer
    $table_header
    $table_hdtr<th>$wims_name_number</th><th>$wims_name_title</th><th>$wims_name_Status</th><th>$wims_name_action</th></tr>
    !read themes/_widgets/supervisorsubclass.phtml
    $table_end
   </div>
 !endif
 !if $seq_open=yes
  !set table_htdr_=$table_hdtr<th>$wims_name_number</th><th>$wims_name_title</th><th>$wims_name_sequence</th><th>$wims_name_Status</th><th>$wims_name_action</th></tr>
 !else
  !set table_htdr_=$table_hdtr<th>$wims_name_number</th><th>$wims_name_title</th><th>$wims_name_Status</th><th>$wims_name_action</th></tr>
 !endif
 !if $[$doccnt+$docpubliccnt]>0
  <div id="wims_supervisor_doc">
   !distribute item 1,no into wims_incremental,table_spacer
   $table_header
  !!<caption>$wims_name_Docs</caption>
   $table_htdr_
   !read themes/_widgets/supervisordoc.phtml
   !read themes/_widgets/supervisordocp.phtml
   $table_end
  </div>
 !endif
 !if $sheetcnt>0
  <div id="wims_supervisor_sheet">
   !distribute item 1,no into wims_incremental,table_spacer
   $table_header
   !!<caption>$wims_name_Sheets</caption>
   $table_htdr_
   !read themes/_widgets/supervisorsheet.phtml
   $table_end
  </div>
 !endif
 !if $examcnt>0
  <div id="wims_supervisor_exam">
   !distribute item 1,no into wims_incremental,table_spacer
   $table_header
   $table_htdr_
   !!<caption>$wims_name_Examens</caption>
   !read themes/_widgets/supervisorexam.phtml
   $table_end
  </div>
 !endif
 !if $votecnt>0
  <div id="wims_supervisor_vote">
   !distribute item 1,no into wims_incremental,table_id,table_spacer
   $table_header
   !!<caption>$wims_name_Votes</caption>
   $table_htdr_
   !read themes/_widgets/supervisorvote.phtml
   $table_end
  </div>
 !endif
 </div>
!endif

</div><!--/wimsbody-->
</div><!--/wimspagebox-->
!read themes/_procs/menumodubox0.proc
!read themes/_widgets/menumodubox_accordion.phtml

:end

!reset div0 div1

!!distribute items div, span into div0,div1
!!read themes/_procs/supervisormenubox.proc
!!read themes/_widgets/menubox.phtml
!!reset div0,div1

</div><!--/wimsbodybox-->

!!read themes/_widgets/visitorcredits.phtml
</body>
</html>
<!-- end of $wims_theme/supervisor.phtml -->
