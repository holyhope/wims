!reset inputsize
!if $wims_read_parm=$empty
 !exit
!endif
!if $wims_read_parm=help
 !read help/$lang/special/codeinput.phtml
 !exit
!endif
!set parm=!replace internal ,	r by $	r in $wims_read_parm
!set parm=!replace internal $	$ by $\
$ in $parm

!set code=!item 1 of $parm
!set code=!declosing $code

!set inputsize=!item 2 to -1 of $parm
!set parm=!line 2 to -1 of $inputsize
!set inputsize=!line 1 of $inputsize

!if noanswer iswordof $inputsize
   !set noanswer=1
!endif
!if div iswordof $inputsize
  !set pre=div
!else
  !set pre=pre
!endif
!set inputsize=!item 1 of $inputsize
!default inputsize=1x5
!set inputsize=!translate x to , in $inputsize
!set inputsize=!items2words $inputsize

!set parmcnt=!linecnt $parm
!set parm=!sort reverse line $parm

!set pretext=!replace internal reply by REPLY in $code

!for tt = 1 to $parmcnt
   !set ld=!line $tt of $parm
   !set upcase=!uppercase $(ld[1])
   !reset inputsize_tmp
   !if $(upcase) isin $pretext
       !set reply_tmp=!nospace $(ld[1])
       !set i_tmp=!text select 0123456789 in $reply_tmp
       !if debug iswordof $m_oefenv and ?analyze notin $(replygood$i_tmp)
         !read oef/special/debug.input $i_tmp
       !endif
       !set rep_tmp=!replace internal reply by r in $reply_tmp
       !set embedded=!append item $rep_tmp to $embedded
       !set inputsize_tmp=$(ld[2])
       !set inputsize_tmp=!translate x to , in $inputsize_tmp
       !if $(inputsize_tmp[2])=$empty and $(inputsize_tmp[1])!=$empty
         !set inputsize_tmp=1,$inputsize_tmp
       !endif
       !set inputsize_tmp=!nospace $inputsize_tmp
       !set inputsize_tmp=!items2words $inputsize_tmp
       !default inputsize_tmp=$inputsize
       !set nbdim=!wordcnt $inputsize_tmp
       !if $nbdim==2
         !distribute words $inputsize_tmp into sizer,sizec
         !if $sizer==1
           !set nbdim=1
           !set inputsize_tmp=$sizec
         !endif
       !endif
       !if $nbdim==2
         !set pretext=!replace internal $upcase by <textarea rows="$sizer" cols="$sizec" name="$reply_tmp">$($reply_tmp)</textarea> in $pretext
       !else
         !set pretext=!replace internal $upcase by <input id="$reply_tmp" size="$inputsize_tmp" name="$reply_tmp" value="$($reply_tmp)" /> in $pretext
       !endif
     !endif
!next

!if $q_form=yes
<$pre class="oef_codeinput">
$pretext
</$pre>
!else
 !set pretext=$code
 !read themes/$wims_theme/oefcolors
 !if $oef_indbad=$empty
   !read themes/oefcolors
 !endif
 !for tt = 1 to $parmcnt
   !set ld=!line $tt of $parm
   !if $(ld[1]) isin $pretext
       !set m_=!text select 0123456789 in $(ld[1])
       !default reply_$m_=$(reply$m_)
       !set tt=!word 1 of $(replygood$m_)
       !if $tt!=?analyze
         !set cl=!nospace oef_ind$(diareply$m_)
         !if $(precreply$m_)=yes and $(diareply$m_)=bad
            !set cl=!nospace oef_indprec
         !endif
         !if $(partialgood$m_)=yes
            !set cl=!nospace oef_indpartial
         !endif
       !else
         !set cl=oef_empty
       !endif
       !set pretext=!replace internal $(ld[1]) by <div class="inline $cl">$(reply_$m_)</div><sup>[$m_]</sup> in $pretext
       !set replyname$m_=[$m_]
   !endif
 !next
<$pre>
$pretext
</$pre>
!endif
