!set methtit=Ajouter un nombre aux deux côtés d'une inégalité
!set methenv=ZZ QQ RR
!set methparmtype=number,parm inequality
!set methhelp=Cette méthode transforme une inégalité du style \(a > b)\
 en l'inégalité équivalente \(a + n > b + n), où \(n) est un nombre.

!if $wims_read_parm iswordof form check
 !goto $wims_read_parm
!endif

!exit
:form
 Ajouter le nombre
 !read deduc/methparm.phtml 1,15
 aux deux côtés de l'inégalité
 !read deduc/methparm.phtml 2

!exit
:check
 !distribute items $methparmobj2 into data,left,sign,right
 methexp=Ajouter \(methparm1) aux deux côtés de \($left $sign $right)

 !read deduc/sub/simplify $left + ($methparm1)\
	$right + ($methparm1)
 !distribute lines $out into newleft,newright
 !if $newleft=$empty or $newright=$empty
  error=bad_data
  !exit
 !endif
 newsign=$sign
 newobject2=$newleft $newsign $newright
 oldobject=2
 
 contextlist=$ct_Always,\
	$ct_Onlywhen le nombre ajouté est positif,\
	$ct_Onlywhen l'inégalité est stricte,\
	$ct_Onlywhen l'inégalité n'est pas stricte

 !if $askresult>1
  !read deduc/sub/simplify $left - ($methparm1)\
	$right - ($methparm1)
  !distribute lines $out into badleft, badright
  badsign=!translate internal <> to >< in $newsign
 
  resultlist=$newobject2,\
	$newleft $newsign $badright,\
	$badleft $newsign $newright,\
	$newleft $badsign $newright,\
	$newleft $badsign $badright
 !endif

!exit

