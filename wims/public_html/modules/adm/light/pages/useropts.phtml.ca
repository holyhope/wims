!!INDEX
!let module_translator=Manel Querol
!let module_translator_address=mquerol@xtec.cat
!let module_title=Configuració de les preferències a WIMS
!let module_description=defineix les teues preferències a WIMS
!let module_language=ca
!!INDEXEND

!let tip=<ul><li><strong>FireFox</strong> nativament suporta MathML sense extensió</li><li><strong>Safari</strong> nativament suporta MathML sense extensió</li><li><strong>Google Chrome</strong> necessita <a target="new" href="https://chrome.google.com/webstore/detail/mathjax-for-chrome/elbbpgnifnallkilnkofjcgjeallfcfa">MathJax Extension </a></li><li><strong>Internet Explorer</strong> necessita <a target="new" href="http://www.dessci.com/en/products/mathplayer/">DESSCI MathPlayer plugin</a></li><li><strong>Opera</strong> necessita <a target="new" href="http://xml-maiden.com/userjs/mathml/">UserJS MathML plugin</a></li></ul>

!read proc/useropts.proc

!header

<p>Aquesta pàgina permet canviar algunes preferències a WIMS.</p>

<div id="widget_useropts">
  <ul>
    <li><a href="#formula">Fórmules matemàtiques</a></li>
    !if $wims_user=$empty
      <li><a href="#language_selector">Llengua per defecte</a></li>
    !endif
    <li><a href="#accessibility">Accessibilitat</a></li>
  </ul>

  !if $wims_user=$empty
  <div id="language_selector">
    <h2>Llengua per defecte</h2>
    <p>
      Tria la llengua de visualització del lloc :
      !read themes/_widgets/language_selector.phtml
    </p>
  </div>
  !endif

  <div id="formula">
    <h2>Fórmules matemàtiques</h2>
    <p>
       El servidor permet dos modes de visualització de fórmules: el Mode imatge, o la utilització de MathML (si el navegador ho suporta).</p>
!!      <p>
      !if $texalign=2
        <p>Actualment, les fórmules es visualitzen en mode MathML.</p>
      !endif
    Alguns exemples :
    <ul>
      <li>
        Lletres gregues:
        <pre>
         !insmath alpha != pi
        </pre>
      </li>
      <li>
        Expressió matemàtica
        !let ins_align=middle
        !! we need to force dynamic insertion
        !set pw=2
        <pre>
        !insmath x_{1,2} = \frac{-b \pm \sqrt{b^{$pw} - 4a c}}{2a}
        </pre>
      </li>
      <li>
        Una desigualtat:
        <pre>
        !insmath | a + b | <= epsilon + 123.45
        </pre>
      </li>
    </ul>

    !if $texalign=2
      <p>
        Si els exemples no es mostren correctament, pots utilitzar un altre
        navegador o utilitzar el servidor en "Mode imatge".
      </p>
        <h3>Mode de visualització</h3>
        <p>
         Ara està en mode MathML
          !set wims_ref_class=wims_button
          !href cmd=resume&useropts=$(arg)0$font Passar a mode imatge
        </p>
        <h3 id="formula_options">Opció de mode MathML :</h3>
          <p>El mode MathML permet un zoom amb un click del ratolí sobre les fórmules.</p>
          !set wims_ref_class=wims_button
          !if $useropts=$texsize$(texalign)0$font
            El zoom està <span class="disabled">desactivat</span>.
            !href cmd=resume&useropts=$texsize$(texalign)1$font#formula_options activar
            <span class="wims_button disabled">desactivar</span>
          !else
            El zoom està <span class="oef_indgood success">activat</span>.
            <span class="wims_button disabled">activar</span>
            !href cmd=resume&useropts=$texsize$(texalign)0$font#formula_options desactivar
          !endif
        <h3>Navegadors compatibles MathML</h3>
        $tip

    !else
      <p>
        Ara el servidor està configurat en <strong>Mode imatge</strong>.<br/> 
        Amb un navegador compatible podeu utilitzar MathML.
      </p>
      <h3>Mode de visualització</h3>
      <p>
        Ara està en mode Imatge
        !set arg=!nospace $texsize 2
        !set wims_ref_class=wims_button
        !href cmd=resume&useropts=$(arg)0$font Passar a mode MathML
      </p>
      <h3>Opcions del mode imatge</h3>
      <ul><li>
          Mida dels símbols i expressions matemàtiques .
         !let ts1=!eval $texsize-1
         !let ts2=!eval $texsize+1
         !if $ts1<1
           !let ts1=1
         !endif
         !if $ts2>9
          !let ts2=9
         !endif
         !set wims_ref_class=wims_button
         !href cmd=resume&useropts=$ts1$texalign$zoom$font#formula <img src="gifs/doc/dgauche.gif" style="width:1em" />
         !set wims_ref_class=wims_button
         !href cmd=resume&useropts=$[$wims_texbasesize-1]$texalign$zoom$font#formula Normal
         !set wims_ref_class=wims_button
         !href cmd=resume&useropts=$ts2$texalign$zoom$font#formula <img src="gifs/doc/ddroite.gif" style="width:1em" />
      </li><li>
          Posició de les expressions matemàtiques. Si l'expressió
          !insmath (x+y)/(x^$pw + y^2)
          és
          !if $texalign=1
           massa alta respecte a la línia de text,
           !set wims_ref_class=wims_button
           !href cmd=resume&useropts=$(texsize)0$zoom$font clica aquí per baixar-la.
          !else
           massa baixa respecte a la línia de text,
           !set wims_ref_class=wims_button
           !href cmd=resume&useropts=$(texsize)1$zoom$font clica aquí per pujar-la.
      !endif
    </li>
    </ul>
    !endif
  </div>

  <div id="accessibility">
    <h2>Accessibilitat</h2>

    <p>Pots ajustar l'espaiat entre lletres i paraules
         !if $useropts=$(texsize)$texalign$(zoom)1
            !set secondary1=wims_button disabled
            !set secondary2=wims_button
          !else
            !set secondary2=wims_button disabled
            !set secondary1=wims_button
          !endif
        !set wims_ref_class=$secondary2
        !href cmd=resume&useropts=$(texsize)$texalign$(zoom)0 Normal

        !set wims_ref_class=$secondary1
        !href cmd=resume&useropts=$(texsize)$texalign$(zoom)1 Lector dislèxic
    </p>
    <p>El mode espaiat és especialment adequat en el cas d'un lector dislèxic.</p>
  </div>

</div>
!if $jquery_defined=yes
  !read adm/tabscript useropts
!endif

<p class="spacer">
  !if $wims_user=$empty
   Quan tot correspongui exactament al que vols, posa
    !set wims_ref_class=wims_button
   <a href="$wims_ref_name?lang=$lang&+useropts=$texsize$texalign$zoom$font" title="WIMS">
   aquest enllaç</a>
   a les Adreces d'interés, per mantenir aquestes preferències per les properes connexions.
  !else
   Les preferències es mantindran en les connexions futures en aquesta classe.
  !endif
</p>

!tail

