
!set wims_form_method=post
!read adm/title.phtml 1\
$classname\
$wims_name_csv

!if $wims_user=supervisor and ($wims_typename iswordof course or \
($wims_typename iswordof class and $wims_supertype iswordof 0 2))
 <p class="wimscenter">
 !set wims_ref_class=wims_button
 !href cmd=reply $name_direct_score
 </p>
!endif

!if $error!=$empty
 <div class="wims_msg alert">
 !read lang/msg.phtml.$moduclass_lang
 </div>
 !if $error iswordof not_secure not_superclass
  !exit
 !endif
<hr />
!endif

!if $outfile!=$empty
 <div class="wims_msg success">
  $name_file <span class="tt wims_fname">
 !getfile $outfile $outfile
 </span> $name_download
 </div>
!endif

!if $processed=yes
 !if $remark$change$added$missing$erased!=$empty or $manual=yes
  <div class="wims_msg success">
  $name_deposit
  <ul>
  !if $change!=$empty
   <li>$name_infochanged: <span class="tt wims_login">
   !replace internal , by ,$ $ in $change
   </span></li>
  !endif
  !if $added!=$empty
   <li>$name_added: <span class="tt wims_login">
   !replace internal , by ,$ $ in $added
   </span></li>
  !endif
  !if $bad_login!=$empty
   <li>$name_badlogin: <span class="tt wims_login">
    !replace internal , by ,$ $ in $bad_login
   </span></li>
  !endif
  !if full iswordof $remark
    <li>$name_warning1</li>
  !endif
  !if noprop iswordof $remark
    <li>$name_warning2</li>
  !endif
  !if noauto iswordof $remark
    <li>$name_warning3</li>
  !endif
  !if $erased!=$empty
    <li>$name_warning4:
    <span class="tt wims_login">
    !replace internal , by ,$ $ in $erased
    </span></li>
  !endif
  !if $missing!=$empty
    <li>
    !if full notwordof $remark
     $name_nologin1:
    !else
     $name_nologin2:
    !endif
    <span class="tt wims_login">$missing</span>.
    !if $pass_missing=yes
     $name_passwarning
    !endif
    </li>
  !endif
  !if $manual=yes
   <li>$name_manual1
   !href module=adm/class/grades $name_check
   .</li>
  !endif
  </ul>
  </div>
 !else
 <div class="wims_msg success">
  $name_data1
  </div>
 !endif
!endif

!form reply
<input type="hidden" name="job" value="csv" />
<input type="hidden" name="csvtype" value="download" />
$name_data2
<ul>
<li>$name_format
 !formselect csvformat list $csvformatlist
 &nbsp;
 !set wims_ref_class=wims_button_help
 !href module=$module&cmd=help&special_parm=csvformats $wims_name_help
</li>

!! keywords of column

<li>
 $name_column
 &nbsp;
 !set wims_ref_class=wims_button_help
 !href module=$module&cmd=help&special_parm=csvfields $wims_name_help

 !readproc csv/keyword.proc
 <div class="field">
 <ul>
 !for bas in basic, basic2
  !if $($bas)!=$empty
   <li>
   !let tmp=!replace password by in $($bas)
   !let tmp=!replace vars by in $($bas)
   !let tmp=!words2items $tmp
   !for k in $tmp
    <input type="checkbox" name="csvdownload" id="csvdownload_$k" value="$k"/><label for="csvdownload_$k">$k</label>
   !next k
   </li>
  !endif
 !next bas
 !let list_type=sh,shexo,ex,exexo,examduration,ma
 !let list_name=$wims_name_Sheets,$name_indicateur,$wims_name_Examens,$wims_name_Examens ($wims_name_Exercices),\
 $wims_name_Examens ($name_Duration),$name_manualscore
 !let list_keyword=sheets,sheetexo,exams,examexo,examduration,manual
 !if $($(list_type[1]))$($(list_type[2]))$($(list_type[3]))$($(list_type[4]))$($(list_type[5]))!=$empty
  <li><input type="checkbox" name="csvdownload" id="csvdownload_allscore" value="allscore"/><label for="csvdownload_allscore">$name_allscore</label></li>
 !endif
 !for t from 1 to 6
    !if $($(list_type[$t]))!=$empty
	  !let tmp=!words2items $($(list_type[$t]))
	  <li>
	 <b>$(list_name[$t])</b>:
	 !if $t notwordof 2 4 5
	 <input type="checkbox" name="csvdownload" id="csvdownload_$(list_keyword[$t])" value="$(list_keyword[$t])"/><label for="csvdownload_$(list_keyword[$t])">$name_all</label>
	 !endif
	 !for k in $tmp
	  <input type="checkbox" name="csvdownload" id="csvdownload_$k" value="$k"/><label for="csvdownload_$k">$k</label>
	 !next k
	</li>
    !endif
 !next t
 !if $av!=$empty
  !let tmp=!words2items $av
  <li><b>$name_Average</b>:
  !for k in $tmp
    <input type="checkbox" name="csvdownload" id="csvdownload_$k" value="$k"/><label for="csvdownload_$k">$k</label>
  !next k
  </li>
 !endif
 <li>
  <input type="checkbox" name="csvdownload" id="csvdownload_techvar" value="techvar"/>
  <label for="csvdownload_techvar">$name_techvar</label>
 </li><li>
  <input type="checkbox" name="csvdownload" id="csvdownload_regvar" value="regvar"/>
  <label for="csvdownload_regvar">$name_regvar</label>
 </li>
 </ul>
</li>
</ul>
<input type="submit" value="$wims_name_download" />
!formend



!! open the upload to the level supervisor alternative use user_supervise
!if $classtype notin 13 or $typename iswordof class course  or\
   ($typename iswordof level and $wims_user=supervisor)
  <hr />
 !set wims_form_method=file
 !form reply
  <input type="hidden" name="job" value="csv" />
  <input type="hidden" name="csvtype" value="upload" />
  $name_data3
  &nbsp;
  !set wims_ref_class=wims_button_help
  !href module=$module&cmd=help&special_parm=upload $wims_name_help
  <ul>
   <li>$name_data <input type="file" size="30" name="wims_deposit" /> <br />
   $name_format2: <span class="tt">.csv</span>, <span class="tt">.tsv</span>, <span class="tt">.txt</span>
   &nbsp;
   !set wims_ref_class=wims_button_help
   !href module=$module&cmd=help&special_parm=csvformats2 $wims_name_help
   </li>
  </ul>
  <input type="submit" value="$wims_name_send" />
 !formend
!endif

!set wims_menu_items=!append line itemsep,0,$wims_name_manage\
usermanage,1,module=adm/class/usermanage\
techvar,1,module=adm/class/techvar\
to $wims_menu_items

!if $wims_user=supervisor and ($wims_typename iswordof course or \
($wims_typename iswordof class and $wims_supertype iswordof 0 2))
  !set wims_menu_items=!append line itemsep,0,$wims_name_Evaluation\
  Score1,1,cmd=resume\
  livret,1,module=adm/class/livret\
to $wims_menu_items
!endif
