
quote="
fbase=data-$wims_class
fbase=!translate internal / to _ in $fbase
fname=wimshome/$wims_sesdir/spreadsheet.$csvformat
dl=!singlespace $csvdownload
dl=!lower $dl
!for t in average,sheet,exam,manual
 dl=!replace $t $ by $t in $dl
!next t
dl=!items2words $dl
dl=!replace word names by name in $dl
dl=!replace grade by manual in $dl
dl=!replace word allscores by allscore in $dl
dl=!replace word name by lastname firstname in $dl
dl=!replace word allscore by averages sheets exams manuals in $dl

basic=login lastname firstname password email regnum comments participate vars
sheetcnttot=!recordcnt wimshome/log/classes/$wims_class/sheets/.sheets
sheetcnt=!itemcnt $activesh
sh=
!for i in $activesh
 sh=$sh sheet$i
 l=!record $i of wimshome/log/classes/$wims_class/sheets/.sheets
 name=!line 3 of $l
 name_sheet$i=$quote$name$quote
!next i
good=$basic $sh

examcnttot=!recordcnt wimshome/log/classes/$wims_class/exams/.exams
examcnt=!itemcnt $activexams
ex=
!for i in $activexams
 ex=$ex exam$i
 l=!record $i of wimshome/log/classes/$wims_class/exams/.exams
 name=!line 4 of $l
 name_exam$i=$quote$name$quote
!next i
good=$good $ex

ma=
l=!record 1 of wimshome/log/classes/$wims_class/.grades
l=!line 2 of $l
!for i=1 to $gcnt
 ma=$ma manual$i
 name=!item $i+2 of $l
 name_manual$i=$quote$name$quote
!next i
good=$good $ma

av=
!if $sheetcnt+$examcnt>0 and $gcnt>0
 av=$av average0
!endif
!if $sheetcnt+$examcnt>0
 av=$av average1
!endif
!if $gcnt>0
 av=$av average2
!endif
good=$good $av

dl=!replace word averages by $av in $dl
dl=!replace word sheets by $sh in $dl
dl=!replace word exams by $ex in $dl
dl=!replace word manuals by $ma in $dl
dl=!words2items $dl
dl=!listuniq $dl
good=!words2items $good
dl_v =!listcomplement $good in $dl
dl_v= !replace internal var_ by in $dl_v
!if $dl_v!=
  dl_var=!replace internal , by ,var_ in var_$(dl_v)
!endif
dl=!listintersect $dl and $good
dl=!nospace $dl
dl_var=!nospace $dl_var
dl_v=!nospace $dl_v
dlcnt=!itemcnt $dl
!if $dlcnt=0
 !exit
!endif

sep_csv=,
sep_xls=;
sep_tsv=$	$
sep=$(sep_$csvformat)
dl2=!replace , by ,user_ in user_$dl
dl4=!replace , by ,user_ in user_$dl_v
dl2=!translate , to $sep in $dl2
!if $dl_var!=
   dl4=$sep$dl4
   dl5=$sep$dl5
   dl=$dl$sep$dl_var
!endif
tit=!replace , by $quote,$quote in $quote$tit$quote
tit=!translate , to $sep in $dl
!for n in login,password,lastname,firstname,email,regnum,comments,participate
 dl2=!replace user_$n by $(quote)user_$n$quote in $dl2
!next n
!for n in $dl_var
 dl4=!replace user_$n by $(quote)user__$n$quote in $dl4
!next n
dl3=!replace user_ by $$name_ in $dl2
dl2=!replace user_ by $$user_ in $dl2
dl5=!replace user_ by $$name__ in $dl4
dl4=!replace user_ by $$user__ in $dl4
scores=no
!if average isin $dl or manual isin $dl or sheet isin $dl or exam isin $dl
 scores=yes
!endif

!read csvnames
!writefile $fname $tit\
$dl3$dl5\

!for u=1 to $usercnt
 !reset user_lastname,user_firstname,user_email,user_regnum,user_comments
 !for n in $dl_v
   !reset user__$n
 !next
 l=!record $u of wimshome/log/classes/$wims_class/.userlist
 user_login=!item 3 of $l
 uu=$user_login
 uuu=!hex $uu
 !reset $basic
 !read adm/class/userdef classes,$wims_class,$user_login
 !defread $userdef
  user_participate=!items2words $user_participate
 !if $scores=yes
  !read adm/class/userscore
  user_average1=$per
  user_average2=$(manual_$uuu)
  !if average0 isitemof $dl
   user_average0=$[rint($manual*$user_average2+(100-$manual)*($per))/100]
  !endif
  !for i=1 to $sheetcnttot
   !if sheet$i isitemof $dl and $i isin $activesh
    p_=!line $i of $percents
    !distribute words $p_ into p1,p2
    !if $p2!=$empty
     !distribute item $[$p1/100],$[$p2/10] into x_,y_
     user_sheet$i=$[rint(100*$scoremax*$(f_$i))/100]
    !else
     user_sheet$i=0
    !endif
   !endif
  !next i
  !for i=1 to $examcnttot
   !if exam$i isitemof $dl and $i isin $activexams
    user_exam$i=$[rint(10*$scoremax*$(es_$i))/100]
   !endif
  !next i
  !for i=1 to $gcnt
   !if manual$i isitemof $dl
    user_manual$i=!item $i of $(Manual_$uuu)
    !default user_manual$i=0
   !endif
  !next i
 !endif
 !appendfile $fname $dl2$dl4
!next u

infile=spreadsheet.$csvformat
outfile=$fbase.$csvformat
!mexec csv/getprep.sh

