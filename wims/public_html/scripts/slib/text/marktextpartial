!if $wims_read_parm!=slib_header
 !goto proc
!endif
slib_title=For marking  some group of words with given explanation
slib_parms=2\
,joker like @_\
,the input text (embedding delimiter: `??')
slib_author=Bernadette PERRIN-RIOU
slib_out= position of words embedded in text by  ?? ?? (without the joker)\
amongst all the groups of the form ?? ?? (one line by embedding);\
[position in the text of the ?? ??];\
Text output with comas ;\
list of explanations (one by groups of words);\
number of words
slib_comment=for use in answer with type=mark ; \
one line by answer (see markword if all the words must be treated in one question).\
only the words with ??etc ?? or ??@_etc ?? are taken in account
slib_example= Je??@_??mange??_|article+nom??une??@_??pomme que j'ai??@_??cueillie sur un??_|article + nom??arbre
!exit

:proc

slib_texte=!lines2words $wims_read_parm
slib_texte=!singlespace $slib_texte
slib_texte= !embraced randitem $slib_texte
slib_texte=!rows2lines $slib_texte
slib_texte=!replace internal $\
$ by &#59; in $slib_texte
slib_texte=!replace internal ?? by ; in $slib_texte

slib_cntrow=!rowcnt $slib_texte
slib_TEXTE=
slib_rep=
slib_mark=
slib_cnt=0
slib_cnt_mark=0
slib_JUSTE=
slib_texte=!replace internal , by &#44; in $slib_texte
slib_texte=!replace internal ( by &#40; in $slib_texte
slib_texte=!replace internal ) by &#41; in $slib_texte
!for slib_i= 1 to $slib_cntrow
  slib_H = !row $slib_i of $slib_texte
  slib_cnt_w=!wordcnt $slib_H
  slib_k=$[$slib_i%2]
  !if $slib_k = 0
    slib_faux=!replace internal | by , in $slib_H
    !distribute items $slib_faux into slib_H,slib_juste
    !if $slib_juste=
      slib_JUSTE = !append item slib_ZZZZZ to $slib_JUSTE
    !else
      slib_JUSTE = !append item $slib_juste to $slib_JUSTE
    !endif
  !endif
  slib_cnt_w=!wordcnt $slib_H
  !if $slib_k = 0
    slib_rep1 = !values $slib_cnt + x for x=1 to $slib_cnt_w
    slib_mark  = !append line $slib_rep1 to $slib_mark
    !if @_ notin $slib_H
     slib_rep1 = !values $slib_cnt_mark + x for x=1 to $slib_cnt_w
     slib_rep  = !append line $slib_rep1 to $slib_rep
    !endif
    slib_cnt_mark=$[$slib_cnt_mark + $slib_cnt_w]
    slib_HH = !words2items $slib_H
    slib_cnt= $[$slib_cnt+1]
  !else
    slib_HH = $slib_H
    slib_cnt= $[$slib_cnt+1]
  !endif
  
  slib_TEXTE = !append item $slib_HH to $slib_TEXTE
!next slib_i
slib_rep=!nonempty items $slib_rep
slib_mark=!nonempty lines $slib_mark

slib_TEXTE = !nonempty items $slib_TEXTE
slib_TEXTE = !replace internal @_ by in $slib_TEXTE
slib_JUSTE = !replace internal slib_ZZZZZ by in $slib_JUSTE

slib_out= $slib_rep;[$slib_mark];$slib_TEXTE;$slib_JUSTE;$slib_cnt
